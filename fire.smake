r"""
Prepare set of files.

Example:
snakemake --directory . --snakefile fire.smake -p --jobs 6 > wf_fire_mnx20181203.log 2>&1

Example for cluster:
screen
unset PYTHONPATH
source activate pyrule
snakemake -s fire.smake --drmaa " -j yes -q short.q -l h_rt=1:00:00" --drmaa-log-dir logs_cluster --jobs 120 --jobname snakejob.{rule}_{wildcards.chem_id}_{jobid}.sh --jobscript sge.sh --latency-wait 120 --keep-going > wf_fire_mnx20181203_run1.log 2>&1

Thomas Duigou, INRA, 2018
"""

# Get compound IDs from "per_chem" folder content
CHEM_IDS, = glob_wildcards("per_chem/{chem_id}.tsv")

# Cluster: prevent specific rules to be executed from cluster nodes
localrules: all, init

rule all:
    input:
        expand('res/{chem_id}.json.gz', chem_id=CHEM_IDS)

rule init:
    output:
        logdir = directory('logs'),
        init_done = touch('init.done')
    shell:
        """
        mkdir -p {output.logdir}
        """

rule fire:
    input:
        init_done = 'init.done',
        chem_file = 'per_chem/{chem_id}.tsv',
        rule_file = 'rules_wish_list.tsv'
    output:
        res_file = 'res/{chem_id}.json.gz'
    log:
        "logs/fire_{chem_id}.log"
    shell:
        """
        python rule_fire.py \
            --ofile {output.res_file} \
            --compress \
            infile --rfile {input.rule_file} --cfile {input.chem_file} \
            > {log} 2>&1
        """
